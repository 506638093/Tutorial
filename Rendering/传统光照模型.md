## Lambert
光源照射到物体表面后，向四面八方反射，产生的漫反射效果。这是一种理想的漫反射光照模型。如下图：这个是顶点函数处理后的该光照模型，因此看起来像素不够平滑。  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165b31650ebc66f2.png)
####  漫反射
　　是投射在粗糙表面上的光向各个方向反射的现象。当一束平行的入射光线射到粗糙的表面时，表面会把光线向着四面八方反射，所以入射线虽然互相平行，由于各点的法线方向不一致，造成反射光线向不同的方向无规则地反射，这种反射称之为“漫反射”或“漫射”。这种反射的光称为漫射光。  
  ![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165b3196ef7f9a99.png)

#### Lambert光照模型计算方法  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165b31b776e91264.png)
漫反射光的强度近似地服从于Lambert定律，即漫反射光的光强仅与入射光的方向和反射点处表面法向夹角的余弦成正比。
原理公式：`diffuse = I*cosθ；`
```c
diffuse = I*(L*N)；
```
#### Unity Shader
```c
/*******************************************
Lambert(兰伯特)光照模型
    1. 理想的漫反射；
    2. 计算都在顶点着色器，因此像素不会平滑过渡；
*******************************************/
Shader "Lambert"
{
    Properties
    {
        _Color("Color", color) = (1.0,1.0,1.0,1.0)
    }
    SubShader{
        Pass{

            Tags { "LightMode" = "ForwardBase"}

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            //使用自定义变量
            uniform float4 _Color;

            //使用Unity定义的变量
            uniform float4 _LightColor0;

            struct vertexInput{
                float4 vertex:POSITION;
                float3 normal:NORMAL;
            };

            struct vertexOutput{
                float4 pos:SV_POSITION;
                float4 col:COLOR;
            };

            //顶点程序
            vertexOutput vert(vertexInput v)
            {
                vertexOutput o;

                float3 normalDirection = normalize( mul(float4(v.normal,0.0),_World2Object).xyz);//将模型空间的法线转到世界空间
                float3 lightDirection;
                float atten = 1.0;

                lightDirection = normalize(_WorldSpaceLightPos0.xyz);//灯光方向
                float3 lightFinal = atten * _LightColor0.xyz *  max(0.0, dot(normalDirection,lightDirection));//计算兰伯特漫反射
                o.col = float4(lightFinal*_Color.rgb,1.0);
                o.pos = mul(UNITY_MATRIX_MVP, v.vertex);
                return o;
            }

            //片段程序
            float4 frag(vertexOutput i):COLOR
            {
                return i.col;
            }

            ENDCG
        }
    }
}
```
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bca749aa3fbd3.png)  
基于顶点的Lambert会产生Gouraud。当然我们也可以将Lambert放到像素上计算。

## 半兰伯特光照模型
实际上，我们在现实世界中经常会发现，即使我们让一个物体不被光直接照射，我们也可能会看到物体，虽然亮度不是很高，这其实是由于物体之间光的反射造成的，也就是间接光照，间接光照是更高级的渲染，比如光线追踪算法等。但是在实时图形学，我们大部分情况是通过一个环境光（Ambient Light）统一代表了间接光，这样，即使在没有光的时候，我们也可以看见物体。
兰伯特光照出来的时候，貌似还没有这么高科技的技术，所以呢，有人就想到了一个取巧的技术（据说是《半条命》）。  
![](/uploads/blog/202101/attach_165bcaac0fb4c373.png)  
通过图片可以看出。原本点乘的结果是（0-1），但我们经过*0.5再+0.5之后就变换到（0.5-1）区间。这样原本黑的地方也就变亮了。
```c
 
Shader "HalfLambert"
{
	//属性
	Properties{
		_Diffuse("Diffuse", Color) = (1,1,1,1)
	}
 
	//子着色器	
	SubShader
	{
		Pass
		{
			//定义Tags
			Tags{ "RenderType" = "Opaque" }
 
			CGPROGRAM
			//引入头文件
			#include "Lighting.cginc"
			//定义Properties中的变量
			fixed4 _Diffuse;
			//定义结构体：应用阶段到vertex shader阶段的数据
			struct a2v
			{
				float4 vertex : POSITION;
				float3 normal : NORMAL;
			};
			//定义结构体：vertex shader阶段输出的内容
			struct v2f
			{
				float4 pos : SV_POSITION;
				float3 worldNormal : TEXCOORD0;
			};
 
			//定义顶点shader
			v2f vert(a2v v)
			{
				v2f o;
				o.pos = mul(UNITY_MATRIX_MVP, v.vertex);
				//把法线转化到世界空间
				o.worldNormal = mul(v.normal, (float3x3)_World2Object);
				return o;
			}
 
			//定义片元shader
			fixed4 frag(v2f i) : SV_Target
			{
				//归一化法线，即使在vert归一化也不行，从vert到frag阶段有差值处理，传入的法线方向并不是vertex shader直接传出的
				fixed3 worldNormal = normalize(i.worldNormal);
				//把光照方向归一化
				fixed3 worldLightDir = normalize(_WorldSpaceLightPos0.xyz);
				//半兰伯特光照，将原来（-1，1）区间的光照条件转化到了（0，1）区间，既保证了结果的正确，又整体提升了亮度，保证非受光面也能有光，而不是全黑
				fixed3 lambert = 0.5 * dot(worldNormal, worldLightDir) + 0.5;
				//最终输出颜色为lambert光强*材质diffuse颜色*光颜色
				fixed3 diffuse = lambert * _Diffuse.xyz * _LightColor0.xyz;
				return fixed4(diffuse, 1.0);
			}
 
			//使用vert函数和frag函数
			#pragma vertex vert
			#pragma fragment frag
 
			ENDCG
		}
 
	}
	//前面的Shader失效的话，使用默认的Diffuse
	FallBack "Diffuse"
}
```
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcad6d27f4a97.png)  
*左边两个是Lambert，右边两个是半Lambert*

## Phong  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcb2ad279a498.png)  
Phong光照模型是一种典型的经验模型。
这也引出我们的计算机图形学第一定律：**如果它看起来是对的，那么它就是对的。**  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcb9fb9ad002d.png)  
之前我们介绍的Lambert光照模型只用到了灯光方向和法线。我们的Phong光照引入了两个新的变量：灯光反射向量和眼睛方向。
我们试想一下。不同的观察角度观看同一物体表面是不是会得到不同的光照结果呢？答案是肯定的。所以眼睛方向也需要参与进来，对吧。
物体表面的镜面反射高光也直接和灯光的反射向量息息相关。

好了。我们现在要解决一个问题：怎么求出灯光的反射向量？

方法一：  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcbf12592119f.png)  
方法二：  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcbf587f7da1b.png)

```c
 fixed4 frag (v2f i) : SV_Target
 {
	 float3 L = normalize(i.lightDir);
	 float3 N = normalize(i.normal);
	 float3 viewDir = normalize(ObjSpaceViewDir(i.objPos));//计算出视线

	float diff = saturate(dot(L, N));//漫反射部分和Lamber一样。灯光和法线的点乘
	float3 reflection = normalize(2.0 * N * diff - L);//反射向量
	float spec = pow(max(0, dot(reflection, viewDir)), _Specular);//高光部分，反射向量和眼睛向量的点乘
	float3 finalSpec = _SpecColor.rgb * spec;
	//漫反射+镜面高光+环境光
	float3 finalLight = diff * _LightColor0 + finalSpec + UNITY_LIGHTMODEL_AMBIENT;

	fixed4 col = tex2D(_MainTex, i.uv);
	return col * float4(finalLight, 1);
}
```
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcc633d17da8d.png)

## Blinn-Phong  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcc7ca186df6d.png)  
我们来看一种情况，当我们的观察向量和反射向量间的夹角不能大于90度。如果点积的结果为负数，镜面光分量会变为0.0。你可能会觉得，当光线与视线夹角大于90度时你应该不会接收到任何光才对，所以这不是什么问题。
而事实上我们的渲染结果就变成了这样：  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcc9f81e96446.png)  
于是我们为了解决这个的问题。就引入了Blinn-Phong模型。Blinn-Phong不在依赖于反射向量，而是采用了所谓的半程向量(Halfway Vector)，即光线与视线夹角一半。当半程向量与法线向量越接近时，镜面光分量就越大。  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bccc1c6b4a235.png)
```c
float spec = 0.0;
if(blinn)
{
	vec3 halfwayDir = normalize(lightDir + viewDir);  
	spec = pow(max(dot(normal, halfwayDir), 0.0), 16.0);
}
else
{
	vec3 reflectDir = reflect(-lightDir, normal);
	spec = pow(max(dot(viewDir, reflectDir), 0.0), 8.0);
}
```
Blinn-Phong和Phong的唯一区别就是在高光部分。  
![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures1/attach_165bccd67232ac9b.png)  

![](https://github.com/506638093/Tutorial/blob/master/Rendering/Textures/attach_165bcce0e2be2d3d.png)  
我们可以看出Blinn-Phong的高光部分会更加锐利。同时也看上去更加真实。
这两种模型都是理想的经验模型。通过前人的经验摸索：塑料我们大多使用Phong光照模型、金属大多使用Blinn-Phong光照模型。
讲完传统光照模型之后。也会后面我们引入PBR做了一些铺垫。
